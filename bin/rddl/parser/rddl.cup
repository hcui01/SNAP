/** RDDL Parser: Definitive Grammar (for use with JavaCUP)
 * 
 *  @author Scott Sanner (ssanner@gmail.com)
 */

package rddl.parser;

import rddl.RDDL;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;

parser code {:
	
	// Just a testing interface (loads formula from file)
	public static void main(String args[]) throws Exception {
	    if (args.length == 0 || args.length > 2) 
			usageAndExit();
	    else if (args.length == 2 && args[1].equalsIgnoreCase("-prefix-output"))
	    	RDDL.USE_PREFIX = true; // Set prefix output
	    else if (args.length == 2) {
	    	System.out.println("Unrecognized option: " + args[1]);
	    	usageAndExit();
	    }
		RDDL rddl = parse(new File(args[0]));
		System.out.println(rddl);
    }

	public static void usageAndExit() {
		System.out.println("Usage: RDDL-filename [-prefix-output]");
	    System.exit(1);	
	}

	// The main runtime method to call
	public static RDDL parse(String s) 
 		throws ParseException {

            Yylex  lex  = null;
		    Symbol rval = null;
            parser p = null;
	        try {
	           lex = new Yylex(new StringReader(s));
	           p = new parser(lex);
		       rval = p.parse();

            } catch (Exception e) {
               int cur_line = (lex != null) ? lex.yyline() + 1 : -1;
               String cur_token = "#" + p.cur_token.sym + ": '" + p.cur_token.value + "'";
               String next_token = "[NOT AVAILABLE]";
               try {
                  Symbol s2 = lex.next_token();
                  next_token = "#" + s2.sym + ": '" + s2.value + "'";
               } catch (Exception e2) { /* discard */ }
	           System.out.println("Error on line " + 
				      ((lex != null) ? ""+ cur_line + ", cur token: " + cur_token + ", next token: " + next_token : "") 
	 			      + "\nException: " + e);
		       //e.printStackTrace();
            }

            return (RDDL)rval.value;		
	}

	// The main runtime method to call
	public static RDDL parse(File f) 
 		throws Exception {

            Yylex  lex  = null;
		    Symbol rval = null;
            parser p = null;
	        try {
	           lex = new Yylex(new FileInputStream(f));
	           p = new parser(lex);
		       rval = p.parse();

            } catch (Exception e) {
               int cur_line = (lex != null) ? lex.yyline() + 1 : -1;
               String cur_token = "#" + p.cur_token.sym + ": '" + p.cur_token.value + "'";
               String next_token = "[NOT AVAILABLE]";
               try {
                  Symbol s2 = lex.next_token();
                  next_token = "#" + s2.sym + ": '" + s2.value + "'";
               } catch (Exception e2) { /* discard */ }
	           System.out.println("Error on line " + 
				      ((lex != null) ? ""+ cur_line + ", cur token: " + cur_token + ", next token: " + next_token : "") 
	 			      + "\nException: " + e);
		       //e.printStackTrace();
            }

            return (RDDL)rval.value;		
	}

:}

/* Define Terminals (and Java types if relevant) */
terminal DEFINE, DOMAIN, INSTANCE, HORIZON, DISCOUNT, OBJECTS, INIT_STATE, 
		 REQUIREMENTS, CONCURRENT, CONTINUOUS, INTEGER_VALUED, LCURLY, RCURLY, 
         MULTIVALUED, INTERMEDIATE_NODES, STATE_ACTION_CONSTRAINTS, UNDERSCORE, 
         TYPES, NEG_INF, POS_INF, CONSTANTS, RELATIONS, MAX_NONDEF_ACTIONS, 
		 PARAM_TYPES, PVARIABLES, TYPE, STATE, DEFAULT, NON_FLUENTS, CASE, 
		 INTERMEDIATE, OBSERVATION, ACTION, RANGE, LEVEL, MIN_NONDEF_INSTANCE,
		 MAX_NONDEF_INSTANCE, SUM_OVER, PROD_OVER, PARAMS, REWARD, FORALL,
		 EXISTS, AND, OR, NOT, KRON_DELTA, DIRAC_DELTA, UNIFORM, BERNOULLI, DISCRETE,
		 NORMAL, POISSON, EXPONENTIAL, GEOMETRIC, PLUS, TIMES, DIV, MINUS, IF, THEN, 
		 ELSE, SWITCH;
terminal SEMI, BANG, LPAREN, RPAREN, DOT, COMMA, LBRACK, QST, COLON,
         RBRACK, LESS, LESSEQ, GREATER, GREATEREQ, COUNT, IMPLY, EQUIV, 
         ASSIGN_EQUAL, COMP_EQUAL, MOD, TRUE, FALSE, NEQ, HIGHEST, UMINUS; 
terminal Integer INTEGER;
terminal Double DOUBLE;
terminal String IDENT, VAR, OBJECT, BOOL, ENUM, INT, REAL, NON_FLUENT, CPFS, CDFS, ENUM_VAL;

/* Define Non-terminals (and Java types) */
non terminal RDDL RDDL_BLOCK;
non terminal RDDL.DOMAIN DOMAIN_BLOCK;
non terminal ArrayList DOMAIN_LIST;
non terminal RDDL.INSTANCE INSTANCE_BLOCK;
non terminal RDDL.NONFLUENTS NONFLUENT_BLOCK;
non terminal RDDL.DOMAIN REQ_SECTION;
non terminal ArrayList TYPE_SECTION;
non terminal ArrayList ENUM_LIST;
non terminal ArrayList TYPE_LIST;
non terminal ArrayList PARAM_LIST;
non terminal ArrayList PARAM_LIST2;
non terminal String TYPE_SPEC;
non terminal RDDL.TYPE_DEF TYPE_DEF;
non terminal ArrayList PVAR_SECTION;
non terminal ArrayList PVAR_LIST;
non terminal RDDL.PVARIABLE_DEF PVAR_DEF;
non terminal ArrayList CPF_SECTION;
non terminal ArrayList CPF_LIST;
non terminal RDDL.CPF_DEF CPF_DEF;
non terminal RDDL.REWARD_DEF REW_SECTION;
non terminal ArrayList STATE_CONS_SECTION;
non terminal ArrayList STATE_CONS_LIST;
non terminal RDDL.STATE_CONS_DEF STATE_CONS_DEF;
non terminal ArrayList OBJECTS_LIST;
non terminal RDDL.OBJECTS_DEF OBJECTS_DEF;
non terminal ArrayList STRING_LIST;
non terminal Object ANY_TYPE;
non terminal Boolean BOOL_TYPE;
non terminal Integer INT_TYPE;
non terminal Double DOUBLE_TYPE;
non terminal String CPF_HEADER;
non terminal RDDL.PVAR_EXPR PVAR_EXPR;
non terminal RDDL.EXPR EXPR;
non terminal RDDL.BOOL_EXPR BOOL_EXPR;
non terminal ArrayList PVAR_INST_LIST;
non terminal RDDL.PVAR_INST_DEF PVAR_INST_DEF;
non terminal ArrayList LCONST_LIST;
non terminal ArrayList TERM_LIST;
non terminal RDDL.LTERM TERM;
non terminal ArrayList TYPED_VAR_LIST;
non terminal RDDL.LTYPED_VAR TYPED_VAR;
non terminal ArrayList ENUM_EXPR_LIST;
non terminal ArrayList CASE_LIST;
non terminal RDDL.CASE CASE_DEF;

/* Precedence Definitions - Highest at bottom */
precedence left ASSIGN_EQUAL;
precedence left EXISTS, FORALL, COUNT;
precedence left EQUIV;
precedence left IMPLY;
precedence left OR;
precedence left AND;
precedence left NOT;
precedence left COMP_EQUAL, NEQ, LESS, LESSEQ, GREATER, GREATEREQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIV, MOD;
precedence left UMINUS;
precedence left HIGHEST;

/* Productions */
start with RDDL_BLOCK;

RDDL_BLOCK ::= DOMAIN_BLOCK:d RDDL_BLOCK:rddl  {: rddl.addDomain(d);   RESULT = rddl; :} 
         | INSTANCE_BLOCK:i RDDL_BLOCK:rddl    {: rddl.addInstance(i); RESULT = rddl; :} 
         | NONFLUENT_BLOCK:n RDDL_BLOCK:rddl   {: rddl.addNonFluents(n); RESULT = rddl; :} 
         | DOMAIN_BLOCK:d        {: RDDL rddl = new RDDL(); rddl.addDomain(d); RESULT = rddl; :} 
         | INSTANCE_BLOCK:i      {: RDDL rddl = new RDDL(); rddl.addInstance(i); RESULT = rddl; :} 
         | NONFLUENT_BLOCK:n     {: RDDL rddl = new RDDL(); rddl.addNonFluents(n); RESULT = rddl; :} 
         ;

DOMAIN_BLOCK ::= DOMAIN IDENT:name LCURLY REQ_SECTION:domain DOMAIN_LIST:list RCURLY 
			{: domain.setName(name); domain.addDefs(list); RESULT = domain; :} 
		 ;
		 
REQ_SECTION ::= REQUIREMENTS ASSIGN_EQUAL LCURLY STRING_LIST:list RCURLY SEMI {: RESULT = new RDDL.DOMAIN(list); :}
		 |  REQUIREMENTS ASSIGN_EQUAL LCURLY RCURLY SEMI {: RESULT = new RDDL.DOMAIN(new ArrayList()); :}
		 ;
		 		 
DOMAIN_LIST ::= TYPE_SECTION:t DOMAIN_LIST:list  {: list.addAll(t); RESULT = list; :} 
		| PVAR_SECTION:p DOMAIN_LIST:list       {: list.addAll(p); RESULT = list; :} 
		| CPF_SECTION:c DOMAIN_LIST:list        {: list.addAll(c); RESULT = list; :} 
		| REW_SECTION:r DOMAIN_LIST:list        {: list.add(0,r);    RESULT = list; :} 
		| STATE_CONS_SECTION:s DOMAIN_LIST:list {: list.addAll(s); RESULT = list; :} 
		| TYPE_SECTION:t   {: RESULT = t; :} 
		| PVAR_SECTION:p   {: RESULT = p; :} 
		| CPF_SECTION:c    {: RESULT = c; :} 
		| REW_SECTION:r    {: RESULT = new ArrayList(Arrays.asList(new Object[] {r})); :} 
		| STATE_CONS_SECTION:s {: RESULT = s; :} 
		;

TYPE_SECTION ::= TYPES LCURLY TYPE_LIST:list RCURLY SEMI {: RESULT = list; :}
		;

TYPE_LIST ::= TYPE_DEF:def            {: RESULT = new ArrayList(Arrays.asList(new Object[] {def})); :} 
		| TYPE_DEF:def TYPE_LIST:list {: list.add(0,def); RESULT = list; :} 
		;

TYPE_DEF ::= IDENT:i COLON OBJECT SEMI {: RESULT = new RDDL.OBJECT_TYPE_DEF(i); :}
		| IDENT:i COLON LCURLY ENUM_LIST:el RCURLY SEMI {: RESULT = new RDDL.ENUM_TYPE_DEF(i,el); :} 
		;

STRING_LIST ::= IDENT:i            {: RESULT = new ArrayList(Arrays.asList(new Object[] {i})); :} 
		| IDENT:i COMMA STRING_LIST:list {: list.add(0,i); RESULT = list; :} 
		;

ENUM_LIST ::= ENUM_VAL:i            {: RESULT = new ArrayList(Arrays.asList(new Object[] {new RDDL.ENUM_VAL(i)})); :} 
		| ENUM_VAL:i COMMA ENUM_LIST:list {: list.add(0,new RDDL.ENUM_VAL(i)); RESULT = list; :} 
		;

ANY_TYPE ::= BOOL_TYPE:t {: RESULT = t; :}
		| DOUBLE_TYPE:t  {: RESULT = t; :}
		| INT_TYPE:t     {: RESULT = t; :}
		| IDENT:i        {: RESULT = i; :}
		| ENUM_VAL:e     {: RESULT = new RDDL.ENUM_VAL(e); :}
		;

BOOL_TYPE ::= TRUE     {: RESULT = new Boolean(true); :}
		| FALSE        {: RESULT = new Boolean(false); :}
		;

DOUBLE_TYPE ::= DOUBLE:d {: RESULT = d; :}
		| MINUS DOUBLE:d {: RESULT = new Double(-d.doubleValue()); :}
		| POS_INF      {: RESULT = new Double(Double.POSITIVE_INFINITY); :}
		| NEG_INF      {: RESULT = new Double(Double.NEGATIVE_INFINITY); :}
		;

INT_TYPE ::= INTEGER:i {: RESULT = i; :}
		| MINUS INTEGER:i {: RESULT = new Integer(-i.intValue()); :}
		| POS_INF      {: RESULT = new Integer(Integer.MAX_VALUE); :}
		| NEG_INF      {: RESULT = new Integer(Integer.MIN_VALUE); :}
		;

PARAM_LIST ::= COLON {: RESULT = new ArrayList(); :}
		| LPAREN PARAM_LIST2:list RPAREN COLON {: RESULT = list; :}
		;

PARAM_LIST2 ::= TYPE_SPEC:t            {: RESULT = new ArrayList(Arrays.asList(new Object[] {t})); :} 
		| TYPE_SPEC:t COMMA PARAM_LIST2:list {: list.add(0,t); RESULT = list; :} 
		;

TYPE_SPEC ::= IDENT:i {: RESULT = i; :} 
		| INT:i       {: RESULT = i; :}
		| REAL:i      {: RESULT = i; :}
		| BOOL:i      {: RESULT = i; :}
		;

PVAR_SECTION ::= PVARIABLES LCURLY PVAR_LIST:list RCURLY SEMI {: RESULT = list; :} 
		;

PVAR_LIST ::= PVAR_DEF:d            {: RESULT = new ArrayList(Arrays.asList(new Object[] {d})); :} 
		| PVAR_DEF:d PVAR_LIST:list {: list.add(0,d); RESULT = list; :} 
		;

PVAR_DEF ::= IDENT:varname PARAM_LIST:list LCURLY STATE COMMA TYPE_SPEC:range COMMA DEFAULT ASSIGN_EQUAL ANY_TYPE:defvalue RCURLY SEMI {: RESULT = new RDDL.PVARIABLE_STATE_DEF(varname, false, range, list, defvalue); :}
		| IDENT:varname PARAM_LIST:list LCURLY NON_FLUENT COMMA TYPE_SPEC:range COMMA DEFAULT ASSIGN_EQUAL ANY_TYPE:defvalue RCURLY SEMI {: RESULT = new RDDL.PVARIABLE_STATE_DEF(varname, true, range, list, defvalue); :}
		| IDENT:varname PARAM_LIST:list LCURLY INTERMEDIATE COMMA TYPE_SPEC:range COMMA LEVEL ASSIGN_EQUAL INTEGER:level RCURLY SEMI {: RESULT = new RDDL.PVARIABLE_INTERM_DEF(varname, range, list, level); :}
		| IDENT:varname PARAM_LIST:list LCURLY OBSERVATION COMMA TYPE_SPEC:range RCURLY SEMI {: RESULT = new RDDL.PVARIABLE_OBS_DEF(varname, range, list); :}
		| IDENT:varname PARAM_LIST:list LCURLY ACTION COMMA TYPE_SPEC:range COMMA DEFAULT ASSIGN_EQUAL ANY_TYPE:defvalue RCURLY SEMI {: RESULT = new RDDL.PVARIABLE_ACTION_DEF(varname, range, list, defvalue); :}
		;

CPF_SECTION ::= CPF_HEADER:ident LCURLY CPF_LIST:list RCURLY SEMI {: list.add(0,new RDDL.CPF_HEADER_NAME(ident)); RESULT = list; :}
		;

CPF_HEADER ::= CPFS:ident {: RESULT = ident; :}
		| CDFS:ident      {: RESULT = ident; :}
		;

CPF_LIST ::= CPF_DEF:d            {: RESULT = new ArrayList(Arrays.asList(new Object[] {d})); :}
		| CPF_DEF:d CPF_LIST:list {: list.add(0,d); RESULT = list; :}
		; 

CPF_DEF ::= PVAR_EXPR:pexpr ASSIGN_EQUAL EXPR:expr SEMI {: RESULT = new RDDL.CPF_DEF(pexpr, expr); :}
		;
		
REW_SECTION ::= REWARD ASSIGN_EQUAL EXPR:expr SEMI {: RESULT = new RDDL.REWARD_DEF(expr); :}
		;

STATE_CONS_SECTION ::= STATE_ACTION_CONSTRAINTS LCURLY STATE_CONS_LIST:list RCURLY SEMI {: RESULT = list; :}
		;

STATE_CONS_LIST ::= STATE_CONS_DEF:d             {: RESULT = new ArrayList(Arrays.asList(new Object[] {d})); :} 
		| STATE_CONS_DEF:d STATE_CONS_LIST:list  {: list.add(0,d); RESULT = list; :} 
		; 

STATE_CONS_DEF ::= BOOL_EXPR:expr SEMI {: RESULT = new RDDL.STATE_CONS_DEF(expr); :}
		;

/* For simplicity in the INSTANCE_BLOCK grammar, order matters. */
/* Also, OBJECTS and NONFLUENTS are the only optional entries. */
INSTANCE_BLOCK ::= INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           NON_FLUENTS ASSIGN_EQUAL IDENT:nonfluents SEMI 
                           OBJECTS LCURLY OBJECTS_LIST:objects RCURLY SEMI 
                           INIT_STATE LCURLY PVAR_INST_LIST:state_list RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,nonfluents,objects,state_list,a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           OBJECTS LCURLY OBJECTS_LIST:objects RCURLY SEMI 
                           INIT_STATE LCURLY PVAR_INST_LIST:state_list RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,null,objects,state_list,a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           NON_FLUENTS ASSIGN_EQUAL IDENT:nonfluents SEMI 
                           INIT_STATE LCURLY PVAR_INST_LIST:state_list RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,nonfluents,null,state_list,a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           INIT_STATE LCURLY PVAR_INST_LIST:state_list RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,null,null,state_list,a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           NON_FLUENTS ASSIGN_EQUAL IDENT:nonfluents SEMI 
                           OBJECTS LCURLY OBJECTS_LIST:objects RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,nonfluents,objects,new ArrayList(),a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           OBJECTS LCURLY OBJECTS_LIST:objects RCURLY SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,null,objects,new ArrayList(),a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           NON_FLUENTS ASSIGN_EQUAL IDENT:nonfluents SEMI 
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,nonfluents,null,new ArrayList(),a,h,d); :} 
		| INSTANCE IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           MAX_NONDEF_ACTIONS ASSIGN_EQUAL INT_TYPE:a SEMI
                           HORIZON ASSIGN_EQUAL INTEGER:h SEMI 
                           DISCOUNT ASSIGN_EQUAL DOUBLE:d SEMI RCURLY
			{: RESULT = new RDDL.INSTANCE(name,domain,null,null,new ArrayList(),a,h,d); :} 
		;

/* For simplicity in the NONFLUENT_BLOCK grammar, order matters. */
/* Also, OBJECTS is the only optional entry. */
NONFLUENT_BLOCK ::= NON_FLUENTS IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           OBJECTS LCURLY OBJECTS_LIST:objects RCURLY SEMI 
                           NON_FLUENTS LCURLY PVAR_INST_LIST:nonfluents_list RCURLY SEMI 
                           RCURLY
			{: RESULT = new RDDL.NONFLUENTS(name,domain,objects,nonfluents_list); :} 
		| NON_FLUENTS IDENT:name LCURLY 
                           DOMAIN ASSIGN_EQUAL IDENT:domain SEMI
                           NON_FLUENTS PVAR_INST_LIST:nonfluents_list SEMI 
                           RCURLY
			{: RESULT = new RDDL.NONFLUENTS(name,domain,null,nonfluents_list); :} 
		;

OBJECTS_LIST ::= OBJECTS_DEF:d            {: RESULT = new ArrayList(Arrays.asList(new Object[] {d})); :}
		| OBJECTS_DEF:d OBJECTS_LIST:list {: list.add(0,d); RESULT = list; :}
		; 

OBJECTS_DEF ::= IDENT:i COLON LCURLY LCONST_LIST:l RCURLY SEMI {: RESULT = new RDDL.OBJECTS_DEF(i, l); :}
		;

PVAR_INST_LIST ::= PVAR_INST_DEF:d   {: RESULT = new ArrayList(Arrays.asList(new Object[] {d})); :}
		| PVAR_INST_DEF:d PVAR_INST_LIST:list {: list.add(0,d); RESULT = list; :}
		; 

PVAR_INST_DEF ::= IDENT:i LPAREN LCONST_LIST:list RPAREN SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, new Boolean(true), list); :}
		| IDENT:i SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, new Boolean(true), new ArrayList()); :}
		| NOT IDENT:i LPAREN LCONST_LIST:list RPAREN SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, new Boolean(false), list); :}
		| NOT IDENT:i SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, new Boolean(false), new ArrayList()); :}
		| IDENT:i LPAREN LCONST_LIST:list RPAREN ASSIGN_EQUAL ANY_TYPE:val SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, val, list); :}
		| IDENT:i ASSIGN_EQUAL ANY_TYPE:val SEMI   {: RESULT = new RDDL.PVAR_INST_DEF(i, val, new ArrayList()); :}
		;

LCONST_LIST ::= IDENT:i                  {: RESULT = new ArrayList(Arrays.asList(new Object[] {new RDDL.OBJECT_VAL(i)})); :} 
		| IDENT:i COMMA LCONST_LIST:list {: list.add(0,new RDDL.OBJECT_VAL(i)); RESULT = list; :} 
		| ENUM_VAL:e               		 {: RESULT = new ArrayList(Arrays.asList(new Object[] {new RDDL.ENUM_VAL(e)})); :} 
		| ENUM_VAL:e COMMA LCONST_LIST:list {: list.add(0,new RDDL.ENUM_VAL(e)); RESULT = list; :} 
		;

TERM ::= IDENT:i  {: RESULT = new RDDL.OBJECT_VAL(i); :}
		| VAR:v   {: RESULT = new RDDL.LVAR(v); :}
		| PVAR_EXPR:p  {: RESULT = new RDDL.TVAR_EXPR(p); :}
		| ENUM_VAL:e   {: RESULT = new RDDL.ENUM_VAL(e); :}
		;

PVAR_EXPR ::= IDENT:i LPAREN TERM_LIST:l RPAREN {: RESULT = new RDDL.PVAR_EXPR(i, l); :}
		| IDENT:i {: RESULT = new RDDL.PVAR_EXPR(i, new ArrayList()); :}
		;

TERM_LIST ::= TERM:t            {: RESULT = new ArrayList(Arrays.asList(new Object[] {t})); :} 
		| TERM:t COMMA TERM_LIST:list {: list.add(0,t); RESULT = list; :} 
		;

TYPED_VAR_LIST ::= TYPED_VAR:t {: RESULT = new ArrayList(Arrays.asList(new Object[] {t})); :}
		| TYPED_VAR:t COMMA TYPED_VAR_LIST:list {: list.add(0,t); RESULT = list; :}
		; 

TYPED_VAR ::= VAR:var COLON IDENT:type {: RESULT = new RDDL.LTYPED_VAR(var, type); :}
		;

ENUM_EXPR_LIST ::= ENUM_VAL:i COLON EXPR:e            {: RESULT = new ArrayList(Arrays.asList(new Object[] {new RDDL.ENUM_VAL(i),e})); :} 
		| ENUM_VAL:i COLON EXPR:e COMMA ENUM_EXPR_LIST:list {: list.add(0,e); list.add(0,new RDDL.ENUM_VAL(i)); RESULT = list; :} 
		;

CASE_LIST ::= CASE_DEF:e            {: RESULT = new ArrayList(Arrays.asList(new Object[] {e})); :} 
		| CASE_DEF:e COMMA CASE_LIST:list {: list.add(0,e); RESULT = list; :} 
		;

CASE_DEF ::= CASE ENUM_VAL:v COLON EXPR:e   {: RESULT = new RDDL.CASE(new RDDL.ENUM_VAL(v),e); :}
		;

BOOL_EXPR ::= PVAR_EXPR:p  {: RESULT = p; :} /* we don't actually know the type yet*/
		| TRUE   {: RESULT = new RDDL.BOOL_CONST_EXPR(true); :}
		| FALSE  {: RESULT = new RDDL.BOOL_CONST_EXPR(false); :}
		| LBRACK BOOL_EXPR:e RBRACK    {: RESULT = e; :}
		| LPAREN BOOL_EXPR:e RPAREN    {: RESULT = e; :}
		| VAR:t1  COMP_EQUAL TERM:t2 {: RESULT = new RDDL.OBJ_COMP_EXPR(new RDDL.LVAR(t1), t2, "=="); :} /* one has to be a var */
/*		| TERM:t1 COMP_EQUAL VAR:t2  {: RESULT = new RDDL.OBJ_COMP_EXPR(t1, new RDDL.LVAR(t2), "=="); :} /* one has to be a var */
		| VAR:t1  NEQ TERM:t2    {: RESULT = new RDDL.OBJ_COMP_EXPR(new RDDL.LVAR(t1), t2, "~="); :} /* one has to be a var */
/*		| TERM:t1 NEQ VAR:t2     {: RESULT = new RDDL.OBJ_COMP_EXPR(t1, new RDDL.LVAR(t2), "~="); :} /* one has to be a var */
		| EXPR:e1 COMP_EQUAL EXPR:e2  {: RESULT = new RDDL.COMP_EXPR(e1, e2, "=="); :}
		| EXPR:e1 NEQ EXPR:e2    {: RESULT = new RDDL.COMP_EXPR(e1, e2, "~="); :}
		| EXPR:e1 GREATER EXPR:e2   {: RESULT = new RDDL.COMP_EXPR(e1, e2, ">"); :}
		| EXPR:e1 GREATEREQ EXPR:e2 {: RESULT = new RDDL.COMP_EXPR(e1, e2, ">="); :}
		| EXPR:e1 LESS EXPR:e2   {: RESULT = new RDDL.COMP_EXPR(e1, e2, "<"); :}
		| EXPR:e1 LESSEQ EXPR:e2 {: RESULT = new RDDL.COMP_EXPR(e1, e2, "<="); :}  
		| NOT BOOL_EXPR:b {: RESULT = new RDDL.NEG_EXPR(b); :} %prec UMINUS
		| BOOL_EXPR:b1 AND BOOL_EXPR:b2 {: RESULT = new RDDL.CONN_EXPR(b1, b2, "^"); :}
		| BOOL_EXPR:b1 OR BOOL_EXPR:b2  {: RESULT = new RDDL.CONN_EXPR(b1, b2, "|"); :}
		| BOOL_EXPR:b1 IMPLY BOOL_EXPR:b2  {: RESULT = new RDDL.CONN_EXPR(b1, b2, "=>"); :}
		| BOOL_EXPR:b1 EQUIV BOOL_EXPR:b2  {: RESULT = new RDDL.CONN_EXPR(b1, b2, "<=>"); :}
		| FORALL UNDERSCORE LCURLY TYPED_VAR_LIST:list RCURLY BOOL_EXPR:b {: RESULT = new RDDL.QUANT_EXPR("forall", list, b); :}
		| EXISTS UNDERSCORE LCURLY TYPED_VAR_LIST:list RCURLY BOOL_EXPR:b {: RESULT = new RDDL.QUANT_EXPR("exists", list, b); :}
/* Now for the random variables */
		| BERNOULLI LPAREN EXPR:prob RPAREN {: RESULT = new RDDL.Bernoulli(prob); :}
/*		| NOISYOR UNDERSCORE LCURLY TYPED_VAR_LIST:list RCURLY BOOL_EXPR:e */
		;

EXPR ::= BOOL_EXPR:b   {: RESULT = b; :} /* Treat boolean expressions as true=1, false=0 */
		| ENUM_VAL:e   {: RESULT = new RDDL.ENUM_VAL(e); :} /* both parameters and pvariable values, we don't actually know the type yet*/
		| INTEGER:i    {: RESULT = new RDDL.INT_CONST_EXPR(i); :}
		| DOUBLE:d     {: RESULT = new RDDL.REAL_CONST_EXPR(d); :}
		| LBRACK EXPR:e RBRACK    {: RESULT = e; :}
		| LPAREN EXPR:e RPAREN    {: RESULT = e; :}
		| PLUS EXPR:e  {: RESULT = e; :} %prec UMINUS 
		| MINUS EXPR:e {: RESULT = new RDDL.OPER_EXPR(new RDDL.INT_CONST_EXPR(0), e, "-"); :} %prec UMINUS
		| EXPR:e1 TIMES EXPR:e2   {: RESULT = new RDDL.OPER_EXPR(e1, e2, "*"); :}
		| EXPR:e1 DIV EXPR:e2     {: RESULT = new RDDL.OPER_EXPR(e1, e2, "/"); :}
		| EXPR:e1 PLUS EXPR:e2    {: RESULT = new RDDL.OPER_EXPR(e1, e2, "+"); :}
		| EXPR:e1 MINUS EXPR:e2   {: RESULT = new RDDL.OPER_EXPR(e1, e2, "-"); :}
		| SUM_OVER UNDERSCORE LCURLY TYPED_VAR_LIST:list RCURLY EXPR:e   {: RESULT = new RDDL.AGG_EXPR("sum", list, e); :}
		| PROD_OVER UNDERSCORE LCURLY TYPED_VAR_LIST:list RCURLY EXPR:e  {: RESULT = new RDDL.AGG_EXPR("prod", list, e); :}
		| IF LPAREN BOOL_EXPR:b RPAREN THEN EXPR:e1 ELSE EXPR:e2         {: RESULT = new RDDL.IF_EXPR(b, e1, e2); :}
		| SWITCH LPAREN PVAR_EXPR:p RPAREN LCURLY CASE_LIST:list RCURLY  {: RESULT = new RDDL.SWITCH_EXPR(p, list); :}
/* Now for the random variables */
		| DIRAC_DELTA LPAREN EXPR:d RPAREN   {: RESULT = new RDDL.DiracDelta(d); :} 
		| KRON_DELTA LPAREN EXPR:k RPAREN    {: RESULT = new RDDL.KronDelta(k); :}
		| UNIFORM LPAREN EXPR:l COMMA EXPR:u RPAREN    {: RESULT = new RDDL.Uniform(l,u); :}
		| NORMAL LPAREN EXPR:mu COMMA EXPR:var RPAREN  {: RESULT = new RDDL.Normal(mu, var); :}
		| EXPONENTIAL LPAREN EXPR:lambda RPAREN  {: RESULT = new RDDL.Exponential(lambda); :} 
		| DISCRETE LPAREN IDENT:enum_type COMMA ENUM_EXPR_LIST:list RPAREN  {: RESULT = new RDDL.Discrete(enum_type, list); :}
		| GEOMETRIC LPAREN EXPR:prob RPAREN      {: RESULT = new RDDL.Geometric(prob); :}
		| POISSON LPAREN EXPR:lambda RPAREN      {: RESULT = new RDDL.Poisson(lambda); :}
/*		| INV_QUADRATIC {:  :} - for Ints, need to build a sampler for this one */
		;



